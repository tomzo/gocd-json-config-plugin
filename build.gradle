apply plugin: 'java'
apply from: "https://raw.githubusercontent.com/gocd/gocd-plugin-gradle-task-helpers/master/helper.gradle?_=${(int) (new Date().toInstant().epochSecond / 60)}"

gocdPlugin {
    id = 'json.config.plugin'
    pluginVersion = '1.0.0'
    goCdVersion = '22.1.0'
    name = 'JSON Configuration Plugin'
    description = 'Configuration plugin that supports GoCD configuration in JSON'
    vendorName = 'Tomasz Setkowski'
    vendorUrl = 'https://github.com/tomzo/gocd-json-config-plugin'

    githubRepo {
        owner = System.getenv('GITHUB_USER') ?: 'bob'
        repo = 'gocd-json-config-plugin'
        token = System.getenv('GITHUB_TOKEN') ?: 'bad-token'
    }

    pluginProject = project

    prerelease = !"No".equalsIgnoreCase(System.getenv('PRERELEASE'))
    assetsToRelease = [project.tasks.jar]
}

group = 'cd.go.plugin.config.json'
version = gocdPlugin.fullVersion(project)

repositories {
    mavenCentral()
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}

ext {
    pluginApiVersion = '25.2.0'
}

dependencies {
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.13.1'
    implementation group: 'org.apache.ant', name: 'ant', version: '1.10.15'
    implementation group: 'com.beust', name: 'jcommander', version: '1.82'

    compileOnly "cd.go.plugin:go-plugin-api:$pluginApiVersion"
    testImplementation "cd.go.plugin:go-plugin-api:$pluginApiVersion"

    testImplementation platform('org.junit:junit-bom:5.12.2')
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine'
    testRuntimeOnly group: 'org.junit.platform', name: 'junit-platform-launcher'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '5.17.0'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '3.0'
}

processResources {
    duplicatesStrategy = DuplicatesStrategy.INCLUDE
    from("resource-templates") {
        filesMatching('*') {
            expand project.pluginDesc
        }
    }
}

test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed", "standardOut"
    }
}

// create a fat jar with all dependencies
jar {
    archiveBaseName = project.name
    preserveFileTimestamps = false
    reproducibleFileOrder = true
    duplicatesStrategy = DuplicatesStrategy.INCLUDE

    manifest {
        attributes 'Main-Class': 'com.tw.go.config.json.cli.JsonPluginCli'
    }

    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }

    exclude "allow.class" // Noise from Ant
    exclude "META-INF/*.txt"
    exclude "META-INF/*.LIST"
    exclude "META-INF/versions/**"
}
